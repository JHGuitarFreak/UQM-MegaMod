; Script generated by the HM NIS Edit Script Wizard.
Unicode True
BrandingText "THE PROCESS IS INCOMPLETE, YET WE HAVE EMERGED."

Var PACKAGEDIR
Var UQMARGS
Var MAKEICON
Var UQMUSERDATA

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "The Ur-Quan Masters MegaMod"
!define PRODUCT_VERSION "0.8.4"
!define PRODUCT_WEB_SITE "http://uqm-mods.sourceforge.net"
!define PRODUCT_FILE_SERVER "https://downloads.sourceforge.net/project/uqm-mods/MegaMod/"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\UrQuanMasters.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_STARTMENU_REGVAL "NSIS:StartMenuDir"
; The INSTALLER_VERSION is a suffix to the version number for installer patches or to mark
; alpha/beta/release candidate status. In normal releases it is the empty string.
!define INSTALLER_VERSION ""

; UQM Package definitions
!include "packages.nsh"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; Start using macros for block structure
!include "LogicLib.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "MegaModWin32.ico"
!define MUI_UNICON "MegaModWin32.ico"
!define MUI_WELCOMEFINISHPAGE_BITMAP "orzshofixti.bmp"
!define MUI_HEADERIMAGE
!define MUI_HEADERIMAGE_BITMAP "ultron.bmp"
!define MUI_HEADERIMAGE_RIGHT
!define MUI_COMPONENTSPAGE_SMALLDESC

; UAC support
RequestExecutionLevel admin

; Welcome page
!define MUI_WELCOMEPAGE_TEXT "Setup will guide you through the installation of $(^NameDA).\r\n\r\nIt is recommended that you close all other applications before starting Setup. This will make it possible to update relevant system files without having to reboot your computer.\r\n\r\n!!!WARNING!!!\r\n\r\n${PRODUCT_NAME} is a standalone game.\r\nIf you install this over any version of UQM it will wipe out the previous version for MegaMod.\r\n\r\n!!!WARNING!!!\r\n\r\n$_CLICK"
!insertmacro MUI_PAGE_WELCOME
; License page
!define MUI_LICENSEPAGE_BUTTON "Install"
!define MUI_LICENSEPAGE_TEXT_BOTTOM "Press the Install button to continue."
!insertmacro MUI_PAGE_LICENSE "COPYING.txt"
; Components page
!define MUI_COMPONENTSPAGE_TEXT_COMPLIST "You can preconfigure the options to mimic the original platforms by selecting those install types. Note that some options change UQM's default configuration and will need to download additional packages."
!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Package Dictory
!define MUI_PAGE_HEADER_TEXT "Choose Package Location"
!define MUI_PAGE_HEADER_SUBTEXT "Choose the folder that holds packages that have already been downloaded."
!define MUI_DIRECTORYPAGE_TEXT_TOP "Setup will look for already-downloaded content packages in the following folder.  To copy them from a different folder, click Browse and select another folder.  If you are doing a net install, leave this field alone. Click Next to continue."
!define MUI_DIRECTORYPAGE_TEXT_DESTINATION "Source Folder"
!define MUI_DIRECTORYPAGE_VARIABLE $PACKAGEDIR
!define MUI_DIRECTORYPAGE_VERIFYONLEAVE
!insertmacro MUI_PAGE_DIRECTORY
; Start menu page
var ICONS_GROUP
!define MUI_STARTMENUPAGE_NODISABLE
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "Games\The Ur-Quan Masters MegaMod"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "${PRODUCT_STARTMENU_REGVAL}"
!insertmacro MUI_PAGE_STARTMENU Application $ICONS_GROUP
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN_NOTCHECKED
!define MUI_FINISHPAGE_NOREBOOTSUPPORT
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\MegaMod-README.txt"
!define MUI_FINISHPAGE_RUN "$INSTDIR\UrQuanMasters.exe"
!define MUI_FINISHPAGE_RUN_PARAMETERS $UQMARGS
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!define MUI_UNCONFIRMPAGE_TEXT_TOP "This program will now uninstall The Ur-Quan Masters MegaMod entirely.  If you wish to preserve content or expansion packs, select Cancel now and back them up.  Otherwise, press Uninstall to continue."
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} v${PRODUCT_VERSION}"
OutFile "uqm-${PRODUCT_VERSION}${INSTALLER_VERSION}-installer.exe"
InstallDir "C:\Games\The Ur-Quan Masters MegaMod\"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show
AllowRootDirInstall true
DirText "Setup will install $(^NameDA) in the following folder. To install in a different folder, click Browse and select another folder.$\r$\n$\r$\n!!!WARNING!!!$\r$\n$\r$\n${PRODUCT_NAME} is a standalone game.$\r$\nIf you install this over any version of UQM it will wipe out the previous version for MegaMod." "" "" "Please select a folder."
InstType "Typical"
InstType "Minimal"
InstType "Mimic PC"
InstType "Mimic 3DO"
InstType "Death 213's Choice"
InstType "No Content"
InstType "All Expansions"

Function .onInit
  Push $0
  StrCpy $PACKAGEDIR $EXEDIR
  StrCpy $UQMARGS ""
  StrCpy $MAKEICON 0
  ReadEnvStr $0 APPDATA
  ${If} $0 == ""
    ReadEnvStr $0 USERPROFILE
    ${If} $0 == ""
      StrCpy $UQMUSERDATA "$INSTDIR\userdata\UQM-MegaMod"
    ${Else}
      ExpandEnvStrings $UQMUSERDATA "%USERPROFILE%\Application Data\UQM-MegaMod"
    ${EndIf}
  ${Else}
    ExpandEnvStrings $UQMUSERDATA "%APPDATA%\UQM-MegaMod"
  ${EndIf}
FunctionEnd

# To use:
# Push the file name.
# Push the installation location.
# It will install it from the Package Directory if necessary; otherwise it
# will download it to a temp file and install that.
Var DOWNLOADTARGET
Var MANDATORY
Var MD5SUM
Var DOWNLOADPATH
Function HandlePackage
  Exch $0 # File location
  Exch
  Exch $1 # File name
  Push $2
  Push $3
  StrCpy $R9 0 # failure count
  # Check to make sure the file wasn't already installed
  ${If} ${FileExists} "$0\$1"
    md5dll::GetFileMD5 "$0\$1"
    Pop $3
    ${If} $MD5SUM == $3
      MessageBox MB_ICONINFORMATION|MB_OK "The package $1 has already been installed."
      Goto PackageDone
    ${EndIf}
  ${EndIf}
  # It's not installed, so check if it's in the package dir.
  SetOutPath "$0"
  SetOverwrite ifdiff
  ${If} ${FileExists} "$PACKAGEDIR\$1"
    md5dll::GetFileMD5 "$PACKAGEDIR\$1"
    Pop $3
    ${If} $MD5SUM != $3
      MessageBox MB_ICONINFORMATION|MB_OKCANCEL "The file $PACKAGEDIR\$1 appears to be corrupt.  The expected MD5 sum was '$MD5SUM', but the actual MD5 sum was '$3'.  Press OK to attempt to download a fresh copy from the distribution site, or Cancel to skip the package." IDOK AttemptDownload IDCANCEL PackageDone
    ${EndIf}
    CopyFiles "$PACKAGEDIR\$1" "$0\$1"
    Goto PackageDone
  ${EndIf}
  # It's not in the package dir, but check if it's there but an over-helpful
  # browser stuck a .zip at the end
  ${If} ${FileExists} "$PACKAGEDIR\$1.zip"
    md5dll::GetFileMD5 "$PACKAGEDIR\$1.zip"
    Pop $3
    ${If} $MD5SUM != $3
      MessageBox MB_ICONINFORMATION|MB_OKCANCEL "The file $PACKAGEDIR\$1.zip appears to be corrupt.  The expected MD5 sum was '$MD5SUM', but the actual MD5 sum was '$3'.  Press OK to attempt to download a fresh copy from the distribution site, or Cancel to skip the package." IDOK AttemptDownload IDCANCEL PackageDone
    ${EndIf}
    CopyFiles "$PACKAGEDIR\$1.zip" "$0\$1"
    Goto PackageDone
  ${EndIf}

  # We're now in a loop of trying to download the file until the user gives
  # up. Since the only way to iterate through the loop more than once is to
  # have the user reply to a message box, this loop is still marked by a
  # label instead of being part of a Do/Loop macro.
AttemptDownload:
  GetTempFileName $DOWNLOADTARGET
  Delete $DOWNLOADTARGET
  CreateDirectory $DOWNLOADTARGET
  inetc::get "$DOWNLOADPATH$1" "$DOWNLOADTARGET/$1" /END
  Pop $2
  ${If} $2 == "OK"
    # Download completed. Confirm the MD5 sum is OK.
    md5dll::GetFileMD5 "$DOWNLOADTARGET\$1"
    Pop $3
    ${If} $MD5SUM != $3
      ${If} $MANDATORY != 0
        StrCpy $3 "THIS IS A MANDATORY PACKAGE.  Without this package, $(^Name) will NOT run."
      ${Else}
        StrCpy $3 "This is an optional package.  $(^Name) will still run, but some content will not be available."
      ${EndIf}
      MessageBox MB_ICONEXCLAMATION|MB_YESNO "The downloaded file $1 doesn't match the internal MD5 sum.  This probably means the download was corrupt.  $3  Do you want to retry from a different mirror?  (Select NO to install the downloaded package anyway - for instance, if you know that the content pack was upgraded or modified since.)" IDYES AttemptDownload
    ${EndIf}
    CopyFiles "$DOWNLOADTARGET\$1" "$0\$1"
  ${Else}
    ${If} $2 == "Cancelled"
      StrCpy $2 "Download was canceled by user."
    ${Else}
      StrCpy $2 "Could not install the package $1 due to the following error: $\"$2$\"."
    ${EndIf}
    ${If} $MANDATORY != 0
      StrCpy $3 "THIS IS A MANDATORY PACKAGE.  Without this package, $(^Name) will NOT run."
    ${Else}
      StrCpy $3 "This is an optional package.  $(^Name) will still run, but some content will not be available."
    ${EndIf}
    MessageBox MB_ICONEXCLAMATION|MB_YESNO "$2  $3  Do you want to retry from a different mirror?" IDYES AttemptDownload
  ${EndIf}
  RmDir /r $DOWNLOADTARGET
PackageDone:
  Pop $3
  Pop $2
  Pop $1
  Pop $0
FunctionEnd

# Usage:
# Push the file name, preferrably a full path.
# Push the string to be appended
# Any errors during appending will be ignored.
Function AppendToFile
  Exch $0 # string to append
  Exch
  Exch $1 # File name
  Push $2 # using $2 for file handle
  FileOpen $2 $1 a
  ${Unless} ${Errors}
    FileSeek $2 0 END  # seek to end
    FileWrite $2 $0
    FileClose $2
  ${EndUnless}
  Pop $2
  Pop $1
  Pop $0
FunctionEnd

Function EnablePrecursorRemixes
  # If there are errors pending AppendToFile will fail
  ClearErrors
  Push "$UQMUSERDATA\uqm.cfg"
  Push "remixmusic = BOOLEAN:true$\r$\n"
  Call AppendToFile
  ClearErrors
FunctionEnd

Function EnableVolsRemixes
  # If there are errors pending AppendToFile will fail
  ClearErrors
  Push "$UQMUSERDATA\megamod.cfg"
  Push "volasMusic = BOOLEAN:true$\r$\n"
  Call AppendToFile
  ClearErrors
FunctionEnd

Function EnableSpaceMusic
  # If there are errors pending AppendToFile will fail
  ClearErrors
  Push "$UQMUSERDATA\megamod.cfg"
  Push "spaceMusic = BOOLEAN:true$\r$\n"
  Call AppendToFile
  ClearErrors
FunctionEnd

Function EnableHD
  # If there are errors pending AppendToFile will fail
  ClearErrors
  Push "$UQMUSERDATA\uqm.cfg"
  Push "resolutionfactor = INT32:2$\r$\n\
      loresBlowupScale = INT32:3$\r$\n\
      reswidth = INT32:1280$\r$\n\
      resheight = INT32:960$\r$\n"
  Call AppendToFile
  ClearErrors
FunctionEnd

# Macros to remove files
!include LogicLib.nsh
!define RemoveFileFromRoot "!insertmacro RemoveFileFromRoot"
!define RemoveDirFromRoot "!insertmacro RemoveDirFromRoot"

Var TempVar

!macro RemoveFileFromRoot File
  StrCpy $TempVar "$INSTDIR\${File}"

  ${If} ${FileExists} $TempVar
    Delete $TempVar
  ${EndIf}

  StrCpy $TempVar ""
!macroend

!macro RemoveDirFromRoot Dir
  StrCpy $TempVar "$INSTDIR\${Dir}"

  ${If} ${FileExists} $TempVar
    RMDir /r $TempVar
  ${EndIf}
  
  StrCpy $TempVar ""
!macroend

Function RemoveHDmodFiles
  ${RemoveDirFromRoot} "doc"
  ${RemoveDirFromRoot} "content\base"
  ${RemoveFileFromRoot} "AUTHORS"
  ${RemoveFileFromRoot} "BUGS"
  ${RemoveFileFromRoot} "build.sh"
  ${RemoveFileFromRoot} "build.vars.in"
  ${RemoveFileFromRoot} "build.zip"
  ${RemoveFileFromRoot} "build2.sh"
  ${RemoveFileFromRoot} "ChangeLog"
  ${RemoveFileFromRoot} "Contributing"
  ${RemoveFileFromRoot} "COPYING"
  ${RemoveFileFromRoot} "dynarri.bmp"
  ${RemoveFileFromRoot} "icon.ico"
  ${RemoveFileFromRoot} "INSTALL"
  ${RemoveFileFromRoot} "INSTALL.macosx"
  ${RemoveFileFromRoot} "INSTALL.mingw"
  ${RemoveFileFromRoot} "INSTALL.msvc"
  ${RemoveFileFromRoot} "INSTALL.symbian"
  ${RemoveFileFromRoot} "jpeg.dll"
  ${RemoveFileFromRoot} "libpng12.dll"
  ${RemoveFileFromRoot} "libpng12-0.dll"
  ${RemoveFileFromRoot} "libtiff-3.dll"
  ${RemoveFileFromRoot} "license.txt"
  ${RemoveFileFromRoot} "Makefile.build"
  ${RemoveFileFromRoot} "Makeinfo"
  ${RemoveFileFromRoot} "Makeproject"
  ${RemoveFileFromRoot} "msvcr100d.dll"
  ${RemoveFileFromRoot} "msvcr110.dll"
  ${RemoveFileFromRoot} "msvcr110_clr0400.dll"
  ${RemoveFileFromRoot} "msvcr110d.dll"
  ${RemoveFileFromRoot} "ogg.dll"
  ${RemoveFileFromRoot} "ogg_d.dll"
  ${RemoveFileFromRoot} "OpenAL32.dll"
  ${RemoveFileFromRoot} "README"
  ${RemoveFileFromRoot} "SDL.dll"
  ${RemoveFileFromRoot} "SDL_gfx.dll"
  ${RemoveFileFromRoot} "SDL_image.dll"
  ${RemoveFileFromRoot} "stderr.txt"
  ${RemoveFileFromRoot} "stdout.txt"
  ${RemoveFileFromRoot} "subst"
  ${RemoveFileFromRoot} "Uninstal.exe"
  ${RemoveFileFromRoot} "uqm.exe"
  ${RemoveFileFromRoot} "uqm.lsm"
  ${RemoveFileFromRoot} "uqm1280 - RUS.bat"
  ${RemoveFileFromRoot} "uqm1280.bat"
  ${RemoveFileFromRoot} "uqm320.bat"
  ${RemoveFileFromRoot} "uqm640.bat"
  ${RemoveFileFromRoot} "uqmdebug.exe"
  ${RemoveFileFromRoot} "uqmdebug.exp"
  ${RemoveFileFromRoot} "uqmdebug.ilk"
  ${RemoveFileFromRoot} "uqmdebug.lib"
  ${RemoveFileFromRoot} "uqmdebug-vs2012.exe"
  ${RemoveFileFromRoot} "uqm-exe-1280.bat"
  ${RemoveFileFromRoot} "uqm-indent"
  ${RemoveFileFromRoot} "vorbis.dll"
  ${RemoveFileFromRoot} "vorbis_d.dll"
  ${RemoveFileFromRoot} "vorbisenc.dll"
  ${RemoveFileFromRoot} "vorbisenc_d.dll"
  ${RemoveFileFromRoot} "vorbisfile.dll"
  ${RemoveFileFromRoot} "vorbisfile_d.dll"
  ${RemoveFileFromRoot} "WhatsNew"
  ${RemoveFileFromRoot} "zlib1.dll"
  ${RemoveFileFromRoot} "content\base.uqm"
  ${RemoveFileFromRoot} "content\menu.key"
  ${RemoveFileFromRoot} "content\uqm.key"
  ${RemoveFileFromRoot} "content\uqm.rmp"
  ${RemoveFileFromRoot} "content\addons\3domusic.zip"
  ${RemoveFileFromRoot} "content\addons\3dovideo.zip"
  ${RemoveFileFromRoot} "content\addons\3dovoice.zip"
  ${RemoveFileFromRoot} "content\addons\hires2x.zip"
  ${RemoveFileFromRoot} "content\addons\hires4x.zip"
FunctionEnd

Function RemoveHDRemixFiles
  ${RemoveFileFromRoot} "AUTHORS"
  ${RemoveFileFromRoot} "COPYING"
  ${RemoveFileFromRoot} "jpeg.dll"
  ${RemoveFileFromRoot} "libpng12.dll"
  ${RemoveFileFromRoot} "license.txt"
  ${RemoveFileFromRoot} "Manual.txt"
  ${RemoveFileFromRoot} "msvcr110.dll"
  ${RemoveFileFromRoot} "msvcr110_clr0400.dll"
  ${RemoveFileFromRoot} "ogg.dll"
  ${RemoveFileFromRoot} "OpenAL32.dll"
  ${RemoveFileFromRoot} "README-SDL.txt"
  ${RemoveFileFromRoot} "README.txt"
  ${RemoveFileFromRoot} "SDL.dll"
  ${RemoveFileFromRoot} "SDL_gfx.dll"
  ${RemoveFileFromRoot} "SDL_image.dll"
  ${RemoveFileFromRoot} "The Urquan Masters.bat"
  ${RemoveFileFromRoot} "tips for newcomers.txt"
  ${RemoveFileFromRoot} "uqm.exe"
  ${RemoveFileFromRoot} "uqmhd remix changelog.txt"
  ${RemoveFileFromRoot} "uqmhd remix readme.txt"
  ${RemoveFileFromRoot} "vorbis.dll"
  ${RemoveFileFromRoot} "vorbisenc.dll"
  ${RemoveFileFromRoot} "vorbisfile.dll"
  ${RemoveFileFromRoot} "WhatsNew"
  ${RemoveFileFromRoot} "zlib1.dll"
  ${RemoveFileFromRoot} "content\menu.key"
  ${RemoveFileFromRoot} "content\uqm.key"
  ${RemoveFileFromRoot} "content\addons\3domusic.uqm"
  ${RemoveFileFromRoot} "content\addons\3dovoice.uqm"
  ${RemoveFileFromRoot} "content\addons\hires2x.uqm"
  ${RemoveFileFromRoot} "content\addons\hires4x.uqm"
  ${RemoveFileFromRoot} "content\packages\uqm-content.uqm"
FunctionEnd

Function RemoveOldFiles
  ClearErrors
  # Delete old content
  # Vanilla UQM Files
  ${RemoveFileFromRoot} "uqm.exe"
  ${RemoveFileFromRoot} "content\addons\uqm-0.8.0-3dovoice.uqm"
  ${RemoveFileFromRoot} "content\packages\uqm-0.8.0-content.uqm"
  # HD-mod Files
  ${If} ${FileExists} "$INSTDIR\uqmdebug-vs2012.exe"
    Call RemoveHDmodFiles
  ${EndIf}
  # HD-mod Remix Files  
  ${If} ${FileExists} "$INSTDIR\The Urquan Masters.bat"
    Call RemoveHDRemixFiles
  ${EndIf}
  # Outdated Addons
  ${RemoveFileFromRoot} "content\addons\yellow-fried-sd.uqm"
  ${RemoveFileFromRoot} "content\addons\yellow-fried-hd.uqm"
  ${RemoveFileFromRoot} "content\addons\uqm-remix-hqspace.uqm"
  ${RemoveFileFromRoot} "content\addons\sol-textures-sd.uqm"
  ${RemoveFileFromRoot} "content\addons\sol-textures-hd.uqm"
  ${RemoveFileFromRoot} "content\addons\rmx-utwig.uqm"
  ${RemoveFileFromRoot} "content\addons\orange-peel-melnorme.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-remix-timing.uqm"
  ${RemoveFileFromRoot} "content\addons\alt-kohr-4x.uqm"
  ${RemoveFileFromRoot} "content\addons\alt-kohr-2x.uqm"
  ${RemoveFileFromRoot} "content\addons\volasaurus-remix-pack.uqm"
  # pre-v0.8.0.85 content
  ${RemoveFileFromRoot} "content\addons\uqm-mm-hd.uqm"
  ${RemoveFileFromRoot} "content\addons\uqm-0.7.0-3DOMusicRemastered.uqm"
  ${RemoveFileFromRoot} "content\addons\uqm-0.7.0-3dovideo.uqm"
  ${RemoveFileFromRoot} "content\packages\mm-0.8.0-content.uqm"
  # v0.8.0.85 content
  ${RemoveFileFromRoot} "content\packages\mm-0.8.0.85-content.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.0.85-hd.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.0.85-vol-space.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.0.85-vols-remix.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.0.85-tarps-choice.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.0.85-sol-textures.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.0.85-rmx-utwig.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.0.85-remix-timing.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.0.85-classic-hd-fonts.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.0.85-3dovoice.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.0.85-yellow-fried.uqm"
  # v0.8.1 content
  ${RemoveFileFromRoot} "content\packages\mm-0.8.1-content.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.1-hd.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.1-vols-remix.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.1-sol-textures.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.1-3dovoice.uqm"
  # v0.8.2 content
  ${RemoveFileFromRoot} "content\packages\mm-0.8.2-content.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.2-hd.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-0.8.2-3dovoice.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-hd-classic-pack.uqm"
  ${RemoveFileFromRoot} "content\addons\mm-vols-remix.uqm"
FunctionEnd

Var IS_VANILLA_DIR
Var MAINFILENAME

!define VANILLA_MD5SUM "59396c1763e295d55343e518a85a1d9f"

Function CheckForVanillaMD5
  StrCpy $IS_VANILLA_DIR "FALSE"
  StrCpy $MAINFILENAME "UrQuanMasters.exe"

  md5dll::GetMD5File "$INSTDIR\uqm.exe"
  Pop $0
  ${If} $0 == ${VANILLA_MD5SUM}
    StrCpy $IS_VANILLA_DIR "TRUE"
    StrCpy $MAINFILENAME "uqm.exe"
  ${EndIf}
FunctionEnd

Function RenameMainEXE
  ${If} ${FileExists} "$INSTDIR\UrQuanMasters.exe"
    Rename "$INSTDIR\UrQuanMasters.exe" "$INSTDIR\$MAINFILENAME"
  ${EndIf}
FunctionEnd

SectionGroup /e "!UQM" SECGRP01
  Section "Executable" SEC01
    SectionIn 1 2 3 4 5 6 7 RO
    SetOutPath "$INSTDIR"

    Call CheckForVanillaMD5
    Call RemoveOldFiles

    SetOverwrite try
    File "AUTHORS.txt"
    File "CHANGELOG.txt"
    File "COPYING.txt"
    File "MegaMod-README.txt"
    File "UQM-Manual.txt"
    File "UQM-README.txt"
    File "README-SDL.txt"
!include "dlls.nsi"

    SetOutPath $UQMUSERDATA
    SetOverwrite try
    File "uqm-pc.cfg"
    File "uqm-3do.cfg"
    File "uqm-kad.cfg"
    File "mm-pc.cfg"
    File "mm-3do.cfg"
    file "mm-kad.cfg"

  ; Shortcuts
    !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    !insertmacro MUI_STARTMENU_WRITE_END

    ${If} $IS_VANILLA_DIR == "TRUE"
      Call RenameMainEXE
    ${EndIf}
  SectionEnd

  Section "Core Data" SEC02
    SectionIn 1 2 3 4 5 7
    CreateDirectory "$INSTDIR\content\addons"
    SetOutPath "$INSTDIR\content"
    SetOverwrite ifnewer
    AddSize ${PKG_CONTENT_SIZE}
    StrCpy $MANDATORY 1
    StrCpy $MD5SUM "${PKG_CONTENT_MD5SUM}"
    File "..\..\content\version"
    StrCpy $DOWNLOADPATH "${PRODUCT_FILE_SERVER}${PRODUCT_VERSION}/Content/"
    Push "${PKG_CONTENT_FILE}"
    Push "$INSTDIR\content\packages"
    Call HandlePackage

    ; Shortcuts
    !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    !insertmacro MUI_STARTMENU_WRITE_END
  SectionEnd

  Section "Desktop Icon" SECICON
    SectionIn 1 2 3 4 5 7
    StrCpy $MAKEICON 1
  SectionEnd
SectionGroupEnd

Section "HD Data" SEC03
  SectionIn 5 7
  AddSize ${PKG_HD_CONTENT_SIZE}
  StrCpy $MANDATORY 0
  StrCpy $MD5SUM "${PKG_HD_CONTENT_MD5SUM}"
  StrCpy $DOWNLOADPATH "${PRODUCT_FILE_SERVER}${PRODUCT_VERSION}/Content/"
  Push "${PKG_HD_CONTENT_FILE}"
  Push "$INSTDIR\content\addons"
  Call HandlePackage
  Call EnableHD
; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section "DOS Mode Data" SECDOS
  SectionIn 3 5 7
  AddSize ${PKG_DOS_MODE_SIZE}
  StrCpy $MANDATORY 0
  StrCpy $MD5SUM "${PKG_DOS_MODE_MD5SUM}"
  StrCpy $DOWNLOADPATH "${PRODUCT_FILE_SERVER}${PRODUCT_VERSION}/Content/"
  Push "${PKG_DOS_MODE_FILE}"
  Push "$INSTDIR\content\addons"
  Call HandlePackage
; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section "3DO Mode Data" SEC3DO
  SectionIn 4 5 7
  AddSize ${PKG_TDO_MODE_SIZE}
  StrCpy $MANDATORY 0
  StrCpy $MD5SUM "${PKG_TDO_MODE_MD5SUM}"
  StrCpy $DOWNLOADPATH "${PRODUCT_FILE_SERVER}${PRODUCT_VERSION}/Content/"
  Push "${PKG_TDO_MODE_FILE}"
  Push "$INSTDIR\content\addons"
  Call HandlePackage
; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

SectionGroup "3DO Content" SECGRP02
  Section "Music" SEC04
    SectionIn 1 4 7
    AddSize ${PKG_3DOMUSIC_SIZE}
    StrCpy $MANDATORY 0
    StrCpy $MD5SUM "${PKG_3DOMUSIC_MD5SUM}"
    StrCpy $DOWNLOADPATH "${PRODUCT_FILE_SERVER}${PRODUCT_VERSION}/Content/"
    Push "${PKG_3DOMUSIC_FILE}"
    Push "$INSTDIR\content\addons"
    Call HandlePackage
  ; Shortcuts
    !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    !insertmacro MUI_STARTMENU_WRITE_END
  SectionEnd

  Section "Voiceovers" SEC05
    SectionIn 1 4 5 7
    AddSize ${PKG_VOICE_SIZE}
    StrCpy $MANDATORY 0
    StrCpy $MD5SUM "${PKG_VOICE_MD5SUM}"
    StrCpy $DOWNLOADPATH "${PRODUCT_FILE_SERVER}${PRODUCT_VERSION}/Content/"
    Push "${PKG_VOICE_FILE}"
    Push "$INSTDIR\content\addons"
    Call HandlePackage
  ; Shortcuts
    !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    !insertmacro MUI_STARTMENU_WRITE_END
  SectionEnd

  Section "Videos" SEC06
    SectionIn 1 4 5 7
    AddSize ${PKG_VIDEO_SIZE}
    StrCpy $MANDATORY 0
    StrCpy $MD5SUM "${PKG_VIDEO_MD5SUM}"
    StrCpy $DOWNLOADPATH "${PRODUCT_FILE_SERVER}${PRODUCT_VERSION}/Content/"
    Push "${PKG_VIDEO_FILE}"
    Push "$INSTDIR\content\addons"
    Call HandlePackage
  ; Shortcuts
    !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    !insertmacro MUI_STARTMENU_WRITE_END
  SectionEnd
SectionGroupEnd

Section "Volasaurus' Complete Remix" SEC07
  SectionIn 5 7
  AddSize ${VOLS_REMIX_SIZE}
  StrCpy $MANDATORY 0
  StrCpy $MD5SUM "${VOLS_REMIX_MD5SUM}"
  StrCpy $DOWNLOADPATH "${PRODUCT_FILE_SERVER}${PRODUCT_VERSION}/Content/"
  Push "${VOLS_REMIX_FILE}"
  Push "$INSTDIR\content\addons"
  Call HandlePackage
  Call EnableVolsRemixes
  Call EnableSpaceMusic
; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section "Volasaurus' Space Music" SEC08
  AddSize ${VOL_SPACE_SIZE}
  StrCpy $MANDATORY 0
  StrCpy $MD5SUM "${VOL_SPACE_MD5SUM}"
  StrCpy $DOWNLOADPATH "${PRODUCT_FILE_SERVER}${PRODUCT_VERSION}/Content/"
  Push "${VOL_SPACE_FILE}"
  Push "$INSTDIR\content\addons"
  Call HandlePackage
  Call EnableSpaceMusic
; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section "Lance_Vader's Utwig voicepack" SEC09
  SectionIn 5 7
  AddSize ${RMX_UTWIG_SIZE}
  StrCpy $MANDATORY 0
  StrCpy $MD5SUM "${RMX_UTWIG_MD5SUM}"
  StrCpy $DOWNLOADPATH "${PRODUCT_FILE_SERVER}${PRODUCT_VERSION}/Content/"
  Push "${RMX_UTWIG_FILE}"
  Push "$INSTDIR\content\addons"
  Call HandlePackage
; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

SectionGroup "Soul Reaver's Fixes/Voices" SECGRP03
  Section "Melnorme Fix/Revoice" SEC10
    SectionIn 7
    AddSize ${MEL_FIX_SIZE}
    StrCpy $MANDATORY 0
    StrCpy $MD5SUM "${MEL_FIX_MD5SUM}"
    StrCpy $DOWNLOADPATH "http://www.warpstormstudios.com/uqmmod/"
    Push "${MEL_FIX_FILE}"
    Push "$INSTDIR\content\addons"
    Call HandlePackage
  ; Shortcuts
    !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    !insertmacro MUI_STARTMENU_WRITE_END
  SectionEnd

  Section "Syreen Fix/Revoice" SEC11
    SectionIn 7
    AddSize ${SYR_FIX_SIZE}
    StrCpy $MANDATORY 0
    StrCpy $MD5SUM "${SYR_FIX_MD5SUM}"
    StrCpy $DOWNLOADPATH "http://www.warpstormstudios.com/uqmmod/"
    Push "${SYR_FIX_FILE}"
    Push "$INSTDIR\content\addons"
    Call HandlePackage
  ; Shortcuts
    !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    !insertmacro MUI_STARTMENU_WRITE_END
  SectionEnd
SectionGroupEnd

SectionGroup "Precursors Remixes" SECGRP04
  Section "Pack 1" SEC12
    SectionIn 7
    AddSize ${PKG_REMIX1_SIZE}
    StrCpy $MANDATORY 0
    StrCpy $MD5SUM "${PKG_REMIX1_MD5SUM}"
    StrCpy $DOWNLOADPATH "https://downloads.sourceforge.net/project/sc2/UQM%20Remix%20Packs/UQM%20Remix%20Pack%201/"
    Push "${PKG_REMIX1_FILE}"
    Push "$INSTDIR\content\addons"
    Call HandlePackage
    Call EnablePrecursorRemixes
  ; Shortcuts
    !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    !insertmacro MUI_STARTMENU_WRITE_END
  SectionEnd

  Section "Pack 2" SEC13
    SectionIn 7
    AddSize ${PKG_REMIX2_SIZE}
    StrCpy $MANDATORY 0
    StrCpy $MD5SUM "${PKG_REMIX2_MD5SUM}"
    StrCpy $DOWNLOADPATH "https://downloads.sourceforge.net/project/sc2/UQM%20Remix%20Packs/UQM%20Remix%20Pack%202/"
    Push "${PKG_REMIX2_FILE}"
    Push "$INSTDIR\content\addons"
    Call HandlePackage
    Call EnablePrecursorRemixes
  ; Shortcuts
    !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    !insertmacro MUI_STARTMENU_WRITE_END
  SectionEnd

  Section "Pack 3" SEC14
    SectionIn 7
    AddSize ${PKG_REMIX3_SIZE}
    StrCpy $MANDATORY 0
    StrCpy $MD5SUM "${PKG_REMIX3_MD5SUM}"
    StrCpy $DOWNLOADPATH "https://downloads.sourceforge.net/project/sc2/UQM%20Remix%20Packs/UQM%20Remix%20Pack%203/"
    Push "${PKG_REMIX3_FILE}"
    Push "$INSTDIR\content\addons"
    Call HandlePackage
    Call EnablePrecursorRemixes
  ; Shortcuts
    !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    !insertmacro MUI_STARTMENU_WRITE_END
  SectionEnd

  Section "Pack 4" SEC15
    SectionIn 7
    AddSize ${PKG_REMIX4_SIZE}
    StrCpy $MANDATORY 0
    StrCpy $MD5SUM "${PKG_REMIX4_MD5SUM}"
    StrCpy $DOWNLOADPATH "https://downloads.sourceforge.net/project/sc2/UQM%20Remix%20Packs/UQM%20Remix%20Pack%204/"
    Push "${PKG_REMIX4_FILE}"
    Push "$INSTDIR\content\addons"
    Call HandlePackage

    AddSize ${PKG_REMIX5_SIZE}
    StrCpy $MANDATORY 0
    StrCpy $MD5SUM "${PKG_REMIX5_MD5SUM}"
    StrCpy $DOWNLOADPATH "${PRODUCT_FILE_SERVER}${PRODUCT_VERSION}/Content/"
    Push "${PKG_REMIX5_FILE}"
    Push "$INSTDIR\content\addons"
    Call HandlePackage
    Call EnablePrecursorRemixes
  ; Shortcuts
    !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    !insertmacro MUI_STARTMENU_WRITE_END
  SectionEnd

SectionGroupEnd

Section -ShortcutsAndIcons
  SetOutPath $INSTDIR
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
    CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\The Ur-Quan Masters MegaMod.lnk" "$INSTDIR\$MAINFILENAME" $UQMARGS
    CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\UQM (Safe Mode).lnk" "$INSTDIR\$MAINFILENAME" "-x --safe"
    CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\UQM (Safe OpenGL).lnk" "$INSTDIR\$MAINFILENAME" "-o --safe"
    CreateDirectory "$SMPROGRAMS\$ICONS_GROUP\Documentation"
    CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation\AUTHORS.lnk" "$INSTDIR\AUTHORS.txt"
    CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation\CHANGELOG.lnk" "$INSTDIR\CHANGELOG.txt"
    CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation\COPYING.lnk" "$INSTDIR\COPYING.txt"
    CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation\Manual.lnk" "$INSTDIR\UQM-Manual.txt"
    CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation\UQM-README.lnk" "$INSTDIR\UQM-README.txt"
    CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation\MegaMod-README.lnk" "$INSTDIR\MegaMod-README.txt"
    CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Keyboard Test.lnk" "$INSTDIR\keyjam.exe"
    CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Saved Games.lnk" "$UQMUSERDATA\save"
    ${If} $MAKEICON = 1
      CreateShortCut "$DESKTOP\The Ur-Quan Masters MegaMod.lnk" "$INSTDIR\$MAINFILENAME" $UQMARGS
    ${EndIf}
    CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk" "$INSTDIR\uninst.exe"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -SetPCConfig
  SectionIn 3
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    SetOutPath $UQMUSERDATA
    Delete "uqm.cfg"
    Delete "megamod.cfg"
    CopyFiles "$UQMUSERDATA\uqm-pc.cfg" "$UQMUSERDATA\uqm.cfg"
    CopyFiles "$UQMUSERDATA\mm-pc.cfg" "$UQMUSERDATA\megamod.cfg"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -Set3DOConfig
  SectionIn 4
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    SetOutPath $UQMUSERDATA
    Delete "uqm.cfg"
    Delete "megamod.cfg"
    CopyFiles "$UQMUSERDATA\uqm-3do.cfg" "$UQMUSERDATA\uqm.cfg"
    CopyFiles "$UQMUSERDATA\mm-3do.cfg" "$UQMUSERDATA\megamod.cfg"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -SetKADConfig
  SectionIn 5
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    SetOutPath $UQMUSERDATA
    Delete "uqm.cfg"
    Delete "megamod.cfg"
    CopyFiles "$UQMUSERDATA\uqm-kad.cfg" "$UQMUSERDATA\uqm.cfg"
    CopyFiles "$UQMUSERDATA\mm-kad.cfg" "$UQMUSERDATA\megamod.cfg"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -SetRemixConfig
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\$MAINFILENAME"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\$MAINFILENAME"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "v${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  Delete "$UQMUSERDATA\uqm-*.cfg"
  Delete "$UQMUSERDATA\mm-*.cfg"
SectionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SECGRP01} "The core executables and content libraries for The Ur-Quan Masters MegaMod."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC01} "Includes the main program, all subsidiary libraries, and basic documentation."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC02} "Graphics, sound, and the PC-edition music for The Ur-Quan Masters MegaMod."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC03} "HD graphics for The Ur-Quan Masters MegaMod."
  !insertmacro MUI_DESCRIPTION_TEXT ${SECDOS} "HD and SD graphics package for the new DOS mode."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC3DO} "HD and SD graphics package for the new 3DO mode."
  !insertmacro MUI_DESCRIPTION_TEXT ${SECICON} "Adds a desktop icon linking directly to The Ur-Quan Masters MegaMod."
  !insertmacro MUI_DESCRIPTION_TEXT ${SECGRP02} "Packages containing music, sound, and video unique to the 1993 3DO release."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC04} "Optional package which includes the remixed songs from the 3DO release."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC05} "Optional package containing the voiceovers from the 3DO release."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC06} "Optional package containing the videos from the 3DO release."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC07} "A complete set of music remixes by Volasaurus.$\r$\nIncludes ambient space music."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC08} "This is the space music portion of Volasaurus' remixes.$\r$\nIt is included in the complete package"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC09} "Lance_Vader's Utwig revoicing that makes the Utwig less whiny and more morose sounding"  
  !insertmacro MUI_DESCRIPTION_TEXT ${SECGRP03} "Soul Reaver's voice packages for the Melnorme and Syreen that more closely match the PC dialog."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC10} "Soul Reaver's revoiced Melnorme package which matches the PC dialog (Voiced by Soul Reaver)"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC11} "Soul Reaver's revoiced Syreen package which matches the PC dialog (Voiced by Katie Otten)"
  !insertmacro MUI_DESCRIPTION_TEXT ${SECGRP04} "Optional content packages containing the official UQM remixes by The Precursors."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC12} `Ur-Quan Masters Remix Pack 1 - 'Super Melee!'  Optional add-on music package.  If this package is selected and not present in the packages directory, the installer will attempt to download it.`
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC13} `Ur-Quan Masters Remix Pack 2 - 'Neutral Aliens - Don't Shoot!'  Optional add-on music package.  If this package is selected and not present in the packages directory, the installer will attempt to download it.`
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC14} `Ur-Quan Masters Remix Pack 3 - 'The Ur-Quan Hierarchy.'  Optional add-on music package.  If this package is selected and not present in the packages directory, the installer will attempt to download it.`
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC15} `Ur-Quan Masters Remix Pack 4 - 'The New Alliance of Free Stars.'  Optional add-on music package.  If this package is selected and not present in the packages directory, the installer will attempt to download it.`
!insertmacro MUI_FUNCTION_DESCRIPTION_END

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  !insertmacro MUI_STARTMENU_GETFOLDER "Application" $ICONS_GROUP
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\content\addons\*.*"
  Delete "$INSTDIR\content\packages\${PKG_CONTENT_FILE}"
  Delete "$INSTDIR\content\version"
  Delete "$INSTDIR\*.*"

!include "undlls.nsi"

  Delete "$SMPROGRAMS\$ICONS_GROUP\*.*"
  Delete "$DESKTOP\The Ur-Quan Masters MegaMod.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\*.*"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Documentation\*.*"

  RMDir "$SMPROGRAMS\$ICONS_GROUP\Documentation"
  RMDir "$SMPROGRAMS\$ICONS_GROUP"
  RMDir "$INSTDIR\content\addons"
  RMDir "$INSTDIR\content\packages\addons\remix"
  RMDir "$INSTDIR\content\packages\addons"
  RMDir "$INSTDIR\content\packages"
  RMDir "$INSTDIR\content"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd
